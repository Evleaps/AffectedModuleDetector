import com.dropbox.affectedmoduledetector.AffectedModuleConfiguration
import com.dropbox.sample.Dependencies

// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    repositories {
        google()
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        mavenLocal()
    }
    dependencies {
        classpath Dependencies.Libs.ANDROID_BUILD_TOOLS
        classpath Dependencies.Libs.KOTLIN_GRADLE_PLUGIN
        classpath Dependencies.Libs.KTLINT
    }
}

apply plugin: "org.jlleitschuh.gradle.ktlint"
apply plugin: "com.dropbox.affectedmoduledetector"

affectedModuleDetector {
    baseDir = "${project.rootDir}"
    pathsAffectingAllModules = [
            "buildSrc/"
    ]
    specifiedBranch = "main"
    compareFrom = "SpecifiedBranchCommit"
    customTasks = [
            new AffectedModuleConfiguration.CustomTask(
                    "runSomeCustomTaskByImpact",
                    "someTaskForExample",
                    "Command someTaskForExample is just an example task for demonstration of" +
                            "how to add custom commands to impact analysis"
            )
    ]
    logFolder = "${project.rootDir}"
    excludedModules = [
            "sample-util"
    ]
}

// just for example; Do not add to you project.
allprojects {
    tasks.register("someTaskForExample") {task ->
        println("someTaskForExample on path: ${task.path} was called")
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

